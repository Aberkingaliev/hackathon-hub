plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.2'
	id 'io.spring.dependency-management' version '1.1.2'
	id 'com.google.protobuf' version '0.9.4'
	id 'jacoco'
}

repositories {
	maven {
		url "https://maven-central.storage-download.googleapis.com/maven2/" }
	mavenCentral()
	mavenLocal()
}

sourceSets {
	main {
		proto {
			srcDir 'src/main/java/com/hackathonhub/serviceuser/protos'
			include '**/*.protodevel'
			include '**/*.proto'
		}
	}
}

def grpcVersion = '1.57.0'
def protobufVersion = '3.23.4'
def protocVersion = protobufVersion

group = 'com.hackathonhub'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

ext {
	set('springCloudVersion', "2022.0.4")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.postgresql:postgresql:42.6.0'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-services:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"
	implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
	runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.grpc:grpc-testing:1.57.2'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

protobuf {
	protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
	plugins {
		grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

jacoco {
	toolVersion = "0.8.9"
	reportsDirectory = file("$buildDir/jacocoReports")
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = false
		html.required = true
	}
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/grpc'
			srcDirs 'build/generated/source/proto/main/java'
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
