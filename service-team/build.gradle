plugins {
	id 'java'
	id 'org.springframework.boot' version '2.6.13'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'com.google.protobuf' version '0.9.4'
}

group = 'com.hackathonhub'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	maven {
		url "https://maven-central.storage-download.googleapis.com/maven2/" }
	mavenCentral()
	mavenLocal()
}

sourceSets {
	main {
		proto {
			srcDir 'src/main/java/com/hackathonhub/serviceteam/protos'
			include '**/*.protodevel'
			include '**/*.proto'
		}
	}
}
ext {
	set('springCloudVersion', "2021.0.5")
}

def grpcVersion = '1.51.0'
def protobufVersion = '3.23.4'
def protocVersion = protobufVersion

dependencies {
	implementation 'net.devh:grpc-server-spring-boot-starter:2.14.0.RELEASE'
	implementation 'net.devh:grpc-server-spring-boot-autoconfigure:2.14.0.RELEASE'
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

protobuf {
	protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
	plugins {
		grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/grpc'
			srcDirs 'build/generated/source/proto/main/java'
		}
	}
}


tasks.named('test') {
	useJUnitPlatform()
}
